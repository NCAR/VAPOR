#version 410 core

in vec4 gl_FragCoord;
layout(location = 0) out vec4 color;

uniform sampler2D  backFaceTexture;
uniform sampler2D  frontFaceTexture;
uniform sampler3D  volumeTexture;
uniform usampler3D missingValueMaskTexture; // !!unsigned integer!!
uniform sampler1D  colorMapTexture;

uniform vec2  dataRanges[2];
uniform vec3  boxExtents[2];
uniform ivec3 volumeDims;        // number of vertices of this volumeTexture
uniform ivec2 viewportDims;      // width and height of this viewport
uniform vec4  clipPlanes[6];     // clipping planes in **un-normalized** model coordinates

uniform bool  lighting;         // apply lighting or not
uniform bool  hasMissingValue;  // has missing values or not
uniform float lightingCoeffs[4]; // lighting parameters

uniform mat4 MV;
uniform mat4 inversedMV;

// 
// Derive helper variables
//
const float EPSILON    = 5e-6f;
vec2  valueRange       = dataRanges[0];         // min and max values of this variable
vec2  colorMapRange    = dataRanges[1];         // min and max values on this color map
vec3  boxMin           = boxExtents[0];         // min coordinates of the bounding box of this volume
vec3  boxMax           = boxExtents[1];         // max coordinates of the bounding box of this volume
float ambientCoeff     = lightingCoeffs[0];
float diffuseCoeff     = lightingCoeffs[1];
float specularCoeff    = lightingCoeffs[2];
float specularExp      = lightingCoeffs[3];
vec3  volumeDimsf      = vec3( volumeDims );
vec3  boxSpan          = boxMax - boxMin;
mat4  transposedInverseMV = transpose(inversedMV);

//
// Input:  normalized value w.r.t. valueRange.
// Output: normalized value w.r.t. colorMapRange.
//
float TranslateValue( in float value )
{
    float orig = value * (valueRange.y - valueRange.x) + valueRange.x;
    return (orig - colorMapRange.x) / (colorMapRange.y - colorMapRange.x);
}

//
// Input:  Location to be evaluated in texture coordinates.
// Output: If this location should be skipped.
// Note:   It is skipped in two cases: 1) it represents a missing value
//                                     2) it is outside of clipping planes
//
bool ShouldSkip( in vec3 tc )
{
#if 0
    if( hasMissingValue && (texture(missingValueMaskTexture, tc).r != 0u) )
        return true;

    vec4 positionModel  = vec4( (boxMin + tc * (boxMax - boxMin)), 1.0f );
    for( int i = 0; i < 6; i++ )
    {
        if( dot(positionModel, clipPlanes[i]) < 0.0f )
            return true;
    }
#endif

    return false;
}

//
// Input:  Location to be evaluated in texture coordinates
// Output: Gradient at that location
//
vec3 CalculateGradient(in vec3 tc)
{
    vec3 h0 = vec3(-0.5 ) / volumeDimsf;
    vec3 h1 = vec3( 0.5 ) / volumeDimsf;
    vec3 h  = vec3( 1.0 );

    if ((tc.x + h0.x) < 0.0) {
        h0.x = 0.0;
        h.x  = 0.5;
    }
    if ((tc.x + h1.x) > 1.0) {
        h1.x = 0.0;
        h.x  = 0.5;
    }
    if ((tc.y + h0.y) < 0.0) {
        h0.y = 0.0;
        h.y  = 0.5;
    }
    if ((tc.y + h1.y) > 1.0) {
        h1.y = 0.0;
        h.y  = 0.5;
    }
    if ((tc.z + h0.z) < 0.0) {
        h0.z = 0.0;
        h.z  = 0.5;
    }
    if ((tc.z + h1.z) > 1.0) {
        h1.z = 0.0;
        h.z  = 0.5;
    }

    vec3 a0, a1;
    a0.x = texture( volumeTexture, tc + vec3(h0.x,0.0f,0.0f) ).r;
    a1.x = texture( volumeTexture, tc + vec3(h1.x,0.0f,0.0f) ).r;
    a0.y = texture( volumeTexture, tc + vec3(0.0f,h0.y,0.0f) ).r;
    a1.y = texture( volumeTexture, tc + vec3(0.0f,h1.y,0.0f) ).r;
    a0.z = texture( volumeTexture, tc + vec3(0.0f,0.0f,h0.z) ).r;
    a1.z = texture( volumeTexture, tc + vec3(0.0f,0.0f,h1.z) ).r;

    return (a1-a0 / h);
}


void main(void)
{
    vec3  lightDirEye   = vec3(0.0, 0.0, 1.0); 

    // Calculate texture coordinates of this frament
    vec2 fragTexture    = gl_FragCoord.xy / vec2( viewportDims );

    vec3 stopEye        = texture( backFaceTexture,  fragTexture    ).xyz;
    vec3 startEye       = texture( frontFaceTexture, fragTexture    ).xyz;
    vec3 rayDirEye      = stopEye - startEye ;
    float rayDirLength  = length( rayDirEye );
    if( rayDirLength < EPSILON )
        discard;
    
    vec3 startModel     = (inverse( MV ) * vec4(startEye, 1.0)).xyz;
    color = vec4( (startModel - boxMin) / (boxMax - boxMin), 0.8 );

    int   nSteps        = int(length( volumeDimsf ) * 3.0);
    vec3  stepSize3D    = rayDirEye / float(nSteps);

    if( ShouldSkip( startEye ) )
    {
        color = vec4( 0.0f );
    }
    else
    {
        vec3  step1Texture = ((inversedMV * vec4(startEye, 1.0)).xyz - boxMin) / boxSpan;
        float step1Value   = texture( volumeTexture, step1Texture ).r;
              color        = texture( colorMapTexture, TranslateValue(step1Value) );
              color.rgb   *= color.a;
    }

    // let's do a ray casting! 
    for( int i = 1; i <= nSteps; i++ )
    {
        if( color.a > 0.999 )  // You can still see something with 0.99...
            break;

        vec3 step2Eye     = startEye + stepSize3D * float( i );
        vec3 step2Texture = ((inversedMV * vec4(step2Eye, 1.0)).xyz - boxMin) / boxSpan;
        if( ShouldSkip( step2Texture ) )
            continue;

        float step2Value   = texture( volumeTexture, step2Texture ).r;
        vec4  backColor    = texture( colorMapTexture, TranslateValue(step2Value) );
        
        // Apply lighting if big enough gradient
        if( lighting )
        {
            vec3 gradientModel = CalculateGradient( step2Texture );
            if( length( gradientModel ) > EPSILON )
            {
                vec3 gradientEye = (transposedInverseMV * vec4( gradientModel, 0.0f )).xyz;
                     gradientEye = normalize( gradientEye );
                float diffuse    = abs( dot(lightDirEye, gradientEye) );
                vec3 viewDirEye  = normalize( -step2Eye );
                vec3 reflectionEye = reflect( -lightDirEye, gradientEye );
                float specular   = pow( max(0.0f, dot( reflectionEye, viewDirEye )), specularExp ); 
                backColor.rgb    = backColor.rgb * (ambientCoeff + diffuse * diffuseCoeff) + 
                                   specular * specularCoeff;
            }
        }   // End lighting

        // Color compositing
        color.rgb += (1.0f - color.a) * backColor.a * backColor.rgb;
        color.a   += (1.0f - color.a) * backColor.a;
    }   // End ray casting

}       // End main()

